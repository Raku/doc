=begin pod

=TITLE role Dateish

=SUBTITLE Object that can be treated as a date

    role Dateish { ... }

Both L<Date|/type/Date> and L<DateTime|/type/DateTime> support accessing a
year, month and day-of-month, as well as related functionality such as
calculating the day of the week.

=head1 Methods

=head2 method year

Defined as:

    method year(Date:D:) returns Int:D

Usage:

    Dateish.year

Returns the year of the date

=head2 method month

Defined as:

    method month(Date:D:) returns Int:D

Usage:

    Dateish.month

Returns the month of the date (1..12)

=head2 method day

Defined as:

    method day(Date:D:) returns Int:D

Usage:

    Dateish.day

Returns the day of the month of the date (1..31)

=head2 method formatter

Defined as:

    method formatter(Dateish:D:)

Usage:

    Dateish.formatter

Returns the formatting function which is used for conversion to
L<Str|/type/Str>. If none was provided at object construction, a
default formatter is used.  In that case the method will return a
Callable type object.

The formatting function is called by L<#method Str> with the invocant as its
only argument.

    my $dt = Date.new('2015-12-31');  # (no formatter specified)
    say $dt.formatter.WHAT;           # (Callable)
    my $us-format = sub ($self) { sprintf "%02d/%02d/%04d", .month, .day, .year given $self; };
    $dt = Date.new('2015-12-31', formatter => $us-format);
    say $dt.formatter.WHAT;           # (Sub)
    say $dt;                          # 12/31/2015

=head2 method is-leap-year

Defined as:

    method is-leap-year($year = self.year) returns Bool:D

Usage:

    Dateish.is-leap-year(YEAR?)

Returns C<True> if C<$year> is a leap year. Can be called as a class method
if the year is provided.

    say Dateish.is-leap-year(2016);           # True
    say Date.new("1900-01-01").is-leap-year;  # False

=head2 method day-of-month

Defined as:

    method day-of-month(Date:D:) returns Int:D

Usage:

    Dateish.day-of-month

Returns the day of the month of the date (1..31). Synonymous to the C<day>
method.

=head2 method day-of-week

Defined as:

    method day-of-week(Date:D:) returns Int:D

Usage:

    Dateish.day-of-week

Returns the day of the week, where 1 is Monday, 2 is Tuesday and Sunday is 7.

=head2 method day-of-year

Defined as:

    method day-of-year(Date:D:) returns Int:D

Usage:

    Dateish.day-of-year

Returns the day of the year (1..366).

=head2 method days-in-month

Defined as:

    method days-in-month(year = self.year, month = self.month) returns Int:D

Usage:

    Dateish.days-in-month(YEAR?, MONTH?)

Returns the number of days in a month, where year and month default to that
of the invocant. If both year and month are provided, it can be called as
a class method.

    say Date.new("2016-01-02").days-in-month;  # 31
    say Dateish.days-in-month(2016, 1);        # 31

=head2 method week

Defined as:

    method week()

Usage:

    Dateish.week

Returns a list of two integers: the year, and the week number. This is because
at the start or end of a year, the week may actually belong to the other year.

    my ($year, $week) = Date.new("2014-12-31").week;
    say $year;      # 2015
    say $week;      # 1
    say Date.new('2015-01-31').week;        # 2015 5

=head2 method week-number

Defined as:

    method week-number(Date:D:) returns Int:D

Usage:

    Dateish.week-number

Returns the week number (1..53) of the date specified by the invocant. The first
week of the year is defined by ISO as the one which contains the fourth day of 
January. Thus, dates early in January often end up in the last week of the prior 
year, and similarly, the final few days of December may be placed in the first 
week of the next year.

    say Date.new("2014-12-31").week-number;   # 1  (first week of 2015)
    say Date.new("2016-01-02").week-number;   # 53 (last week of 2015)

=head2 method week-year

Defined as:

    method week-year(Date:D:) returns Int:D

Usage:

    Dateish.week-year

Returns the week year of the date specified by the invocant. Normally C<week-year>
is equal to C<Date.year>. Note however that dates early in January often end up in 
the last week of the prior year, and similarly, the final few days of December 
may be placed in the first week of the next year.

    say Date.new("2015-11-15").week-year;   # 2015 
    say Date.new("2014-12-31").week-year;   # 2015 (date belongs to the first week of 2015)
    say Date.new("2016-01-02").week-year;   # 2015 (date belongs to the last week of 2015)

=head2 method weekday-of-month

Defined as:

    method weekday-of-month(Date:D:) returns Int:D

Usage:

    Dateish.weekday-of-month

Returns a number (1..5) indicating the number of times a particular day-of-week
has occurred so far during that month, the day itself included. 

    say Date.new("2003-06-09").weekday-of-month;  # 2  (second Monday of the month)

=head2 method yyyy-mm-dd

Defined as:

    method yyyy-mm-dd(Date:D:) returns Str:D

Usage:

    Dateish.yyyy-mm-dd

Returns the date in C<YYYY-MM-DD> format (L<ISO 8601|http://en.wikipedia.org/wiki/ISO_8601>)

=end pod
