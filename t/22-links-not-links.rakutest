#!/usr/bin/env raku

=begin overview

Avoid I<bare> URLs that are not links; URLs should go inside C<L<>> markup.

=end overview

use experimental :rakuast;

use Test;
use RakuDoc::Test::Files;

my @files = RakuDoc::Test::Files.pods;

if @files {
    plan +@files;
} else {
    plan :skip-all<No rakudoc files specified>
}

sub walk($node) {
    my @children;
    if $node ~~ RakuAST::Doc::Paragraph {
        @children = $node.atoms;
    } elsif $node ~~ RakuAST::Doc::Block {
        return if $node.type eq 'code'|'implicit-code'|'comment'|'table';
        @children = $node.paragraphs;
    } elsif $node ~~ RakuAST::Doc::Markup {
        return if $node.letter eq 'L';
        @children = $node.atoms;
    } else {
        # If this hits, need to adapt test
        flunk "new node type: $node.^name";
    }

    for @children -> $child {
        if $child ~~ Str {
            if $child ~~ / $<url>=[ 'http' 's'? '://' <-[/]>* '/'? ] / {
                flunk "URL found: $<url>";
            }
        } else {
            walk($child);
        }
    }
}

for @files -> $file {
    %*ENV<RAKUDO_RAKUAST>=1;
    subtest $file => {
        for $file.IO.slurp.AST.rakudoc -> $pod {
            walk($pod);
        }
    }
}
