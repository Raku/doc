#!/usr/bin/env raku

use Test;
use RakuDoc::Test::Files;

# Every .rakudoc file in the Type directory.
my @files = RakuDoc::Test::Files.pods.grep(* ~~ /Type | Language/);

if @files {
    plan +@files;
} else {
    plan :skip-all<No rakudoc files specified>
}

for @files -> $file {
    my @lines;
    my Int $line-no = 1;
    for $file.IO.lines -> $line {
        if so $line ~~ /(multi|method|sub) .+? ')' \s+? 'returns' \s+? (<alnum>|':')+? $/ {
            @lines.push($line-no);
        }
        $line-no++;
    }
    if @lines {
        flunk "$file uses 'returns' at lines: {@lines}; should be -->";
    } else {
        pass "$file return types are ok";
    }
}
