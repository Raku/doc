#!/usr/bin/env raku

=begin overview

Check any L<> references for validity.

=end overview

use Test;
use lib $*PROGRAM.parent(2).child('lib');

use Test-Files;
use Pod::Convenience;

my @files = Test-Files.pods;

if @files {
    plan +@files;
} else {
    plan :skip-all<No rakudoc files specified>
}

sub is-valid-c($contents) {
    for @$contents -> $string {
        is $string, $string.trim, "C<$string> no extra whitespace.";
    }
}

sub walk-content($x) {
    for $x.contents -> $contents {
        next unless $contents;
        for @$contents -> $item {
            if $item ~~ Pod::FormattingCode and $item.type eq 'C' {
                is-valid-c($item.contents);
            } elsif $item !~~ Str {
                walk-content($item);
            }
        }
    }
}

for @files -> $file {
    my @chunks = extract-pod($file).contents;

    # This emits pass or flunk for each local L<> found.
    subtest $file => {
        walk-content($_) for @chunks;
    }
}
